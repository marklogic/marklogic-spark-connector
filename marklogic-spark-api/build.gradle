/**
 * This has a number of "api" dependencies so that the libraries are available on the compile classpath for the
 * marklogic-langchain4j and marklogic-spark-connector projects.
 */
dependencies {
  api("com.marklogic:marklogic-client-api:7.2.0") {
    // Need to use the versions of Jackson preferred by Spark.
    exclude group: "com.fasterxml.jackson.core"
    exclude group: "com.fasterxml.jackson.dataformat"
  }

  // This is compileOnly as Spark will provide its own copy at runtime.
  compileOnly "com.fasterxml.jackson.core:jackson-databind:2.17.2"

  // For logging.
  implementation "org.slf4j:jcl-over-slf4j:2.0.17"

  // Needed for splitting XML documents via XPath.
  implementation "jaxen:jaxen:2.0.0"

  // Needed for classifying documents via Semaphore.
  api("com.smartlogic.csclient:Semaphore-CS-Client:${semaphoreVersion}") {
    exclude group: "com.fasterxml.jackson.core"
  }
  api("com.smartlogic.cloud:Semaphore-Cloud-Client:${semaphoreVersion}") {
    exclude group: "com.fasterxml.jackson.core"
  }

  // Adding this in 2.6.0. tika-core is very small and only brings in commons-io and and slf4j-api. Flux can then
  // include the necessary parsers.
  api "org.apache.tika:tika-core:${tikaVersion}"

  // Needed for using XmlMapper.
  api("com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.17.2") {
    // Not needed, as the modules in this group that this dependency depends on are all provided by Spark.
    exclude group: "com.fasterxml.jackson.core"
  }

  // Supports reading and writing RDF data. Including this here so it's available to the tests as well.
  // Bumped to 5.x, which requires Java 17, while upgrading Spark to 4.x.
  api("org.apache.jena:jena-arq:5.5.0") {
    exclude group: "com.fasterxml.jackson.core"
    exclude group: "com.fasterxml.jackson.dataformat"
  }

  // Needed for some XML operations that are far easier with JDOM2 than with DOM.
  api "org.jdom:jdom2:2.0.6.1"
}
